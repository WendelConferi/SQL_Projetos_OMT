USE [DAX_OMT_PRD]
GO

/****** Object:  View [BiView].[ConsumoMateriaPrima_ComConsumoRealTerceiros_OMT]    Script Date: 31/08/2022 07:55:02 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




CREATE VIEW [BiView].[ConsumoMateriaPrima_ComConsumoRealTerceiros_OMT] AS

SELECT 	

	CONVERT(DATE, T.POSTEDDATETIME) AS "Data", 
	DATA.MESANO AS "Data (ano/mês)",
	II.ITEMGROUPID AS "Grupo",
	I.COSTGROUPID AS "Grupo de Custos",
	I.REQGROUPID AS "Grupo de Cobertura",
	P.PRODID AS "Ordem de Produção",
--<INICIO; Autor: Wendel; Data: 2022/01/03>
/*
 * Adicionado 
 * Coluna "Propriedades" para poder filtrar materiais de "terceiros" e "OMT"
 */

	'OMT' AS "Propriedade",    
	 
--<FIM;>
	CONCAT(L.ITEMID, ' - ', PT.NAME) AS "Item",
	PR.SEARCHNAME AS "Item - Nome de Pesquisa",
	L.BOMCONSUMP AS "Consumo", 
	
	CASE WHEN L.BOMUNITID = 'Kg' 
		THEN 1.0 
		ELSE I.NETWEIGHT 
	END AS "Peso",
	
	CASE WHEN L.BOMUNITID = 'Kg' 
		THEN L.BOMCONSUMP 
		ELSE L.BOMCONSUMP * I.NETWEIGHT 
	END  AS "Total"

FROM PRODJOURNALBOM L
	JOIN PRODJOURNALTABLE T ON T.JOURNALID = L.JOURNALID AND T.JOURNALTYPE = 0
	JOIN INVENTDIM D ON D.INVENTDIMID = L.INVENTDIMID
	JOIN PRODTABLE P ON P.PRODID = L.PRODID
	JOIN INVENTTABLE I ON I.ITEMID = L.ITEMID AND I.DATAAREAID = L.DATAAREAID
	JOIN ECORESPRODUCT PR ON PR.RECID = I.PRODUCT
	JOIN ECORESPRODUCTTRANSLATION PT ON PT.PRODUCT = PR.RECID AND PT.LANGUAGEID = 'PT-BR'
	CROSS APPLY BiUtil.DataFiltroAnoMes(CONVERT(DATE, T.POSTEDDATETIME)) DATA
	JOIN INVENTITEMGROUPITEM II ON II.ITEMDATAAREAID = I.DATAAREAID AND II.ITEMID = I.ITEMID
WHERE II.ITEMGROUPID IN ('010', '020', '080')
	AND T.POSTED = 1
	AND T.DATAAREAID = 'OMT'
	AND L.BOMCONSUMP > 0

UNION

SELECT 

	CONVERT(DATE, PJ.POSTEDDATETIME) AS "Data", 
	DATA.MESANO AS "Data (ano/mês)",
	II.ITEMGROUPID AS "Grupo",
	I.COSTGROUPID AS "Grupo de Custos",
	I.REQGROUPID AS "Grupo de Cobertura",
	P.PRODID AS "Ordem de Produção",

--<INICIO; Autor: Wendel; Data: 2022/01/03>
/*
 * Adicionado 
 * Coluna "Propriedades" para poder filtrar materiais de "terceiros" e "OMT"
 */

	'Terceiro' AS "Propriedade",

--<FIM;>
	
	CONCAT(I.ITEMID, ' - ', PT.NAME) AS "Item",
	PR.SEARCHNAME AS "Item - Nome de Pesquisa",

	SUM((C.CONSUMPVARIABLE + CONSUMPCONSTANT)) / SUM(C.QTY) * SUM(PP.QTYGOOD + PP.QTYERROR) AS "Consumo",
	
	CASE WHEN C.UNITID = 'Kg' 
		THEN 1.0 
		ELSE I.NETWEIGHT 
	END AS "Peso",
	
	CASE WHEN C.UNITID = 'Kg' 
		THEN (SUM(C.CONSUMPVARIABLE + CONSUMPCONSTANT)) / SUM(C.QTY) * SUM(PP.QTYGOOD + PP.QTYERROR) 
		ELSE (SUM(C.CONSUMPVARIABLE + CONSUMPCONSTANT)) / SUM(C.QTY) * SUM(PP.QTYGOOD + PP.QTYERROR) * I.NETWEIGHT
	END  AS "Total"

FROM PRODCALCTRANS C
	JOIN PRODTABLE P ON P.PRODID = C.TRANSREFID AND P.COLLECTREFPRODID = C.COLLECTREFPRODID
	JOIN PRODJOURNALTABLE PJ ON PJ.PRODID = P.PRODID
	JOIN PRODJOURNALPROD PP ON PP.JOURNALID = PJ.JOURNALID AND PJ.JOURNALTYPE = 1
	JOIN INVENTDIM D ON D.INVENTDIMID = C.INVENTDIMID
	JOIN INVENTTABLE I ON I.ITEMID = C.RESOURCE_ AND I.DATAAREAID = C.DATAAREAID
	JOIN ECORESPRODUCT PR ON PR.RECID = I.PRODUCT
	JOIN ECORESPRODUCTTRANSLATION PT ON PT.PRODUCT = PR.RECID AND PT.LANGUAGEID = 'PT-BR'
	JOIN INVENTITEMGROUPITEM II ON II.ITEMDATAAREAID = I.DATAAREAID AND II.ITEMID = I.ITEMID
	CROSS APPLY BiUtil.DataFiltroAnoMes(CONVERT(DATE, PJ.POSTEDDATETIME)) DATA
WHERE 1=1
	AND II.ITEMGROUPID = '150'
	--AND P.PRODSTATUS IN ()
	AND P.DATAAREAID = 'OMT'
	AND CONVERT(DATE, PJ.POSTEDDATETIME) < '2016-08-23'
GROUP BY CONVERT(DATE, PJ.POSTEDDATETIME), DATA.MESANO, P.PRODID, II.ITEMGROUPID, I.ITEMID, PT.NAME, PR.SEARCHNAME, C.UNITID, I.NETWEIGHT, I.COSTGROUPID, I.REQGROUPID
HAVING SUM(C.QTY) > 0

UNION

SELECT 	

	CONVERT(DATE, T.POSTEDDATETIME) AS "Data", 
	DATA.MESANO AS "Data (ano/mês)",
	II.ITEMGROUPID AS "Grupo",
	I.COSTGROUPID AS "Grupo de Custos",
	I.REQGROUPID AS "Grupo de Cobertura",
	P.PRODID AS "Ordem de Produção",

--<INICIO; Autor: Wendel; Data: 2022/01/03>
/*
 * Adicionado 
 * Coluna "Propriedades" para poder filtrar materiais de "terceiros" e "OMT"
 */

	'Terceiro' AS "Propriedade",

--<FIM;>
	
	CONCAT(L.ITEMID, ' - ', PT.NAME) AS "Item",
	PR.SEARCHNAME AS "Item - Nome de Pesquisa",
	L.BOMPROPOSAL AS "Consumo", 
	
	CASE WHEN L.BOMUNITID = 'Kg' 
		THEN 1.0 
		ELSE I.NETWEIGHT 
	END AS "Peso",
	
	CASE WHEN L.BOMUNITID = 'Kg' 
		THEN L.BOMPROPOSAL
		ELSE L.BOMPROPOSAL * I.NETWEIGHT 
	END  AS "Total"

FROM PRODJOURNALBOM L
	JOIN PRODJOURNALTABLE T ON T.JOURNALID = L.JOURNALID AND T.JOURNALTYPE = 0
	JOIN INVENTDIM D ON D.INVENTDIMID = L.INVENTDIMID
	JOIN PRODTABLE P ON P.PRODID = L.PRODID
	JOIN INVENTTABLE I ON I.ITEMID = L.ITEMID AND I.DATAAREAID = L.DATAAREAID
	JOIN ECORESPRODUCT PR ON PR.RECID = I.PRODUCT
	JOIN ECORESPRODUCTTRANSLATION PT ON PT.PRODUCT = PR.RECID AND PT.LANGUAGEID = 'PT-BR'
	CROSS APPLY BiUtil.DataFiltroAnoMes(CONVERT(DATE, T.POSTEDDATETIME)) DATA
	JOIN INVENTITEMGROUPITEM II ON II.ITEMDATAAREAID = I.DATAAREAID AND II.ITEMID = I.ITEMID
WHERE II.ITEMGROUPID IN ('150')
	AND T.POSTED = 1
	AND T.DATAAREAID = 'OMT'
	AND L.BOMPROPOSAL > 0
	AND CONVERT(DATE, T.POSTEDDATETIME) >= '2016-08-23'







GO
